CLASS:: ArrayMax
summary:: detect the largest value (and its position) in an array of UGens
categories:: UGens>Multichannel, UGens>Analysis
related:: Classes/ArrayMin, Classes/BufMax

DESCRIPTION::
Finds the maximum value in an Array of UGens, and outputs the value and the index.

This is for use in a SynthDef. If you simply need to find out which is the maximum in an array of numbers, use the link::Overviews/Methods#maxIndex#maxIndex:: and/or link::Overviews/Methods#maxItem#maxItem:: methods defined for any Collection.


CLASSMETHODS::

METHOD:: ar
argument:: array
returns:: # val, index

METHOD:: kr
argument:: array
returns:: # val, index


EXAMPLES::

Here we generate a three-channel signal containing some simple test curves, and analyse it to generate a plot:

code::
s.boot;
(
{
	var son, val, index;
	son = [
		Line.ar(10, 1, 0.5),  // a linear descent in half a second
		XLine.ar(1, 10, 1),   // an exponential ascent in one second
		DC.ar(3)              // a constant
		];
	# val, index = ArrayMax.ar(son);
	[val, index]
}.plot(1, minval: nil, maxval: nil);
)
::

Here we use the operation as an audio effect:

code::
(
x = {
	// A collection of different tones:
	var son = SinOsc.ar((100, 100.3 .. 110));
	var val = ArrayMax.ar(son).at(0);
	val = LeakDC.ar(val); // the operation tends to induce DC offset
	Pan2.ar(val)
}.play
)
x.free
::
